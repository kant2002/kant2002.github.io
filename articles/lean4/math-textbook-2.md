---
layout: page
title: Lean 4 із підручником з математики - Частина 2 - Підмножини
permalink: /lean4/math-textbook-2/
use_math: true
---

*Це переклад першої статті від Филип Уайшчак (Filip Łajszczak) - [https://filip.lajszczak.dev/lean-4-with-a-math-textbook---part-1---defining-sets-and-membership.html](https://filip.lajszczak.dev/lean-4-with-a-math-textbook---part-1---defining-sets-and-membership.html) про формалізацію класичної теорії множин від Хелени Расьової у Lean4*

> Ми продовжуємо систематичне дослідження книги Гелени Расьової «Вступ до сучасної математики» за допомогою формалізації в Lean 4 [Дивіться вступ](/lean4/math-textbook-0/). Визначивши в [Частині 1](/lean4/math-textbook-1/), як працюють множини та належність через кодування предикатами, тепер переходимо до відношень підмножин.

## Відношення підмножин: Формула (5)

Визначення Расьови:

> Якщо кожен елемент множини $A$ є елементом множини $B$, тоді ми говоримо, що $𝐴 \in$ підмножиною $B$. Також кажуть, що множина $A$ міститься в множині $B$, або що $B$ містить $A$. Це записується як $A \subseteq B$ або $B \supset A$.

Потім вона ставить формальну умову:

> За означенням, $A \subseteq B$  тоді й тільки тоді, коли для кожного $x$ виконується така умова: якщо $x \in A$, то $x \in B$.

У її символічному записі:

$$(A \subseteq B) \leftrightarrow (\textit{for every } x : x \in A
\Rightarrow x \in B) \tag{5}$$

**Це безпосередньо перекладається як Lean:**

```lean
def subset (A B : RasiowaSet α) : Prop := ∀ x, x ∈ A → x ∈ B
infixr:50 " ⊆ " => subset
infixr:50 " ⊇ " => fun A B => B ⊆ A  -- superset notation: 
                                     -- A ⊇ B means B ⊆ A
```

**Справжні (власні) підмножини**: Расьова зазначає у примітці, що коли множини $A$ і $B$ не $\in$ тотожними, «кажуть, що $A \in$ власною підмножиною $B$». Відношення власної підмножини означає, що $A$ міститься в $B$, але множини $A$ і $B$ не рівні - існує принаймні один елемент у $B$, якого немає в $A$.

```lean
- Proper subset: A ⊂ B means A ⊆ B and A ≠ B (Rasiowa's footnote)
def proper_subset (A B : RasiowaSet α) : Prop := A ⊆ B ∧ A ≠ B
infixr:50 " ⊂ " => proper_subset
```

Повна відповідність: і Расьова, і Lean виражають підмножину через універсальну квантифікацію (∀ - «для всіх») над імплікацією (→ - «якщо… то»). Математичний зміст є тотожним.

Зв’язок у явному вигляді: хоча Lean автоматично розуміє, що наша інфіксна нотація `𝐴 ⊆ 𝐵` означає те саме, що й логічна формула `∀𝑥, 𝑥 ∈ 𝐴 → 𝑥 ∈ 𝐵`, з навчальною метою корисно сформулювати цю еквівалентність явно:

```lean
theorem subset_def (A B : RasiowaSet α) :
  A ⊆ B ↔ ∀ x, x ∈ A → x ∈ B := by rfl
```

Ця теорема доводить еквівалентність за допомогою `rfl` (рефлексивності), оскільки обидві сторони є визначально рівними - інфіксна нотація `⊆` є лише синтаксичним цукром, що розгортається в логічну формулу. Це явне твердження допомагає з’єднати математичну нотацію з формальною логікою, показуючи, що означення з підручника Расьової та наше кодування в Lean виражають ідентичний математичний зміст.

Расьова наводить кілька конкретних прикладів:

> Множина всіх цілих чисел міститься в множині всіх раціональних чисел, оскільки кожне ціле число є раціональним. Множина $A = \lbrace 1, 2 \rbrace$ міститься в множині $B = \lbrace 1, 2, 3 \rbrace $, оскільки $ 1 \in B % і 2 ∈ 𝐵. \[…] Множина всіх ірраціональних чисел міститься в множині всіх дійсних чисел, оскільки кожне ірраціональне число є дійсним.

# Коли множини не є підмножинами: Формула (6)

Расіова також пояснює, коли множини не є підмножинами:

> Твердження про те, що $A$ не є підмножиною $B$, записується як $A \nsubseteq B$ або $B \nsupseteq A$. \[…] Із означення підмножини випливає, що $A \nsubseteq $B$ тоді й тільки тоді, коли не кожен елемент множини 𝐴 є елементом множини 𝐵, тобто в множині 𝐴 існує такий елемент, який не належить множині 𝐵. У символічній нотації:

$$\sim(A \subseteq B) \Leftrightarrow (\textit{there is an } x \textit{ such that: } x \in A \textit{ and } \sim(x \in B)) \tag{6}$$

Вона наводить такий приклад: «Множина всіх цілих чисел, кратних 3, не міститься в множині всіх цілих чисел, кратних 6, оскільки існує ціле число, кратне 3, але не кратне 6, наприклад число 9».

**Тактики Lean Proof, які ми використовуватимемо:**

Тактика `constructor` ([див.](https://lean-lang.org/doc/reference/latest//Tactic-Proofs/Tactic-Reference/#constructor)) розділяє двосторонні твердження (якщо й тільки якщо, ↔) на дві окремі цілі: пряму імплікацію (→) та обернену (←).

Тактика `intro` ([див.](https://lean-lang.org/doc/reference/latest//Tactic-Proofs/Tactic-Reference/#intro)) працює з припущеннями: для універсальних кванторів (∀) вона вводить змінні, а для імплікацій (→) приймає гіпотезу.

Тактика `rewrite` ([див.](https://lean-lang.org/doc/reference/latest//Tactic-Proofs/Tactic-Reference/#rewrite)) замінює вирази, використовуючи рівність або еквівалентність. Синтаксис `rewrite [theorem_name] at hypothesis` застосовує переписування до конкретної гіпотези.

Тактика `cases` ([див.](https://lean-lang.org/doc/reference/latest//Tactic-Proofs/Tactic-Reference/#cases)) виконує розбір випадків для індуктивних типів, таких як диз’юнкції (∨), існування (∃) та кон’юнкції (∧).

Тактика `have` вводить проміжні результати. Синтаксис `have name : type := proof` створює нову гіпотезу, яку можна використовувати на наступних кроках.

Тактика `exact` ([див.](https://lean-lang.org/doc/reference/latest///Tactic-Proofs/Tactic-Reference/#exact)) надає довідковий терм, що точно збігається з поточною ціллю. Її застосовують тоді, коли ми маємо саме той доказ, який потрібен для завершення кроку.

Крапки (`·`) - це маркери Lean, які допомагають структурувати доведення, чітко розділяючи різні випадки чи підцілі.

Ключове слово `theorem` створює іменований доказ, який можна використовувати пізніше. На відміну від `example` (яке є анонімним), теореми стають частиною нашої математичної бібліотеки і можуть застосовуватися в інших доведеннях.

```lean
theorem not_subset_iff (A B : RasiowaSet α) :
  ¬(A ⊆ B) ↔ ∃ x, (x ∈ A ∧ x ∉ B) := by
  constructor  -- Split bidirectional proof 
               -- into forward and reverse directions
  · -- Forward: ¬(A ⊆ B) → ∃ x, (x ∈ A ∧ x ∉ B)
    intro h  -- Assume h : ¬(A ⊆ B)
    rewrite [subset_def] at h  -- Use our explicit theorem to unfold:
                               -- h : ¬(∀ x, x ∈ A → x ∈ B)
    rewrite [not_forall] at h  -- Apply logical equivalence:
                               -- h : ∃ x, ¬(x ∈ A → x ∈ B)
    cases h with  -- Extract witness from existential
    | intro x hx =>  -- hx : ¬(x ∈ A → x ∈ B)
      rewrite [not_imp] at hx  -- Apply ¬(P → Q) ↔ (P ∧ ¬Q):
                               -- hx : x ∈ A ∧ x ∉ B
      exact ⟨x, hx⟩  -- Provide witness x with proof hx
  · -- Reverse: ∃ x, (x ∈ A ∧ x ∉ B) → ¬(A ⊆ B)
    intro h  -- Assume h : ∃ x, (x ∈ A ∧ x ∉ B)
    cases h with  -- Extract witness from existential
    | intro x hx =>  -- hx : x ∈ A ∧ x ∉ B
      cases hx with  -- Destructure conjunction
      | intro hx_in_A hx_not_in_B =>  -- hx_in_A : x ∈ A,
                                      -- hx_not_in_B : x ∉ B
        intro h_subset  -- Assume h_subset : A ⊆ B (for contradiction)
        have hx_in_B : x ∈ B := h_subset x hx_in_A  -- Apply subset 
                                                    -- to get x ∈ B
        exact hx_not_in_B hx_in_B  -- Contradiction: x ∉ B but x ∈ B
```

Цей доказ встановлює логічну еквівалентність, яку описує Расьова: $A$ не є підмножиною $B$ тоді й лише тоді, коли існує деякий елемент у $A$, який не належить $B$.

Пройдемо цей доказ крок за кроком звичайною математичною мовою, щоб показати відповідність між формальним і неформальним міркуванням:

**Прямий напрямок**: ми хочемо показати, що якщо $A$ не є підмножиною $B$, тоді існує деякий елемент у $A$, який не належить $B$.

Припустимо, що $A$ не є підмножиною $B$. За означенням це означає, що неправда, ніби кожен елемент $A$ належить $B$. У класичній логіці «не кожен» еквівалентно «існує такий, що … не», отже, повинен існувати деякий елемент $x$, для якого $x \in A$, але $x \in B$. Це саме те, що ми хотіли довести.

**Зворотній напрямок**: ми хочемо показати, що якщо існує деякий елемент у 𝐴, який не належить 𝐵, тоді 𝐴 не є підмножиною 𝐵.

Припустимо, що існує конкретний елемент 𝑥 такий, що $x \in A$ і $x \notin B$. Тепер, для доведення від супротивного, вважатимемо, що $A \subseteq B$. Тоді за означенням підмножини кожен елемент 𝐴 повинен належати 𝐵. Зокрема, оскільки $x \in A$, мусить виконуватися й $x \in B$. Але ми вже знаємо, що $x \notin B$ - суперечність! Отже, наше припущення було хибним, і маємо $A \nsubseteq B$.

# Рівність множин: Формула (7)

Расіова визначає, коли два множини рівні:

> Множини $A$ та $B$ є ідентичними тоді і тільки тоді, коли вони мають однакові елементи. Це записується так:

$$(A = B) \Leftrightarrow (\textit{for every } x : x \in A \Leftrightarrow x \in B) \tag{7}$$

У Lean це теорема про екстенсивність множин:

```lean
theorem set_extensionality (A B : RasiowaSet α) :
  A = B ↔ ∀ x, (x ∈ A ↔ x ∈ B) := by
  constructor  -- Split bidirectional proof into forward and reverse
  · -- Forward direction: if A = B, then same membership
    intro h x  -- Assume h : A = B, let x be arbitrary
    rw [h]     -- Rewrite goal using A = B: 
               -- x ∈ A ↔ x ∈ B becomes x ∈ B ↔ x ∈ B
               -- which is trivially true by reflexivity
  · -- Reverse direction: if same membership, then A = B
    intro h    -- Assume h : ∀ x, (x ∈ A ↔ x ∈ B)
    funext x   -- Function extensionality: 
               -- to prove A = B (functions equal),
               -- show A x = B x for all x
    exact propext (h x)  -- Propositional extensionality: 
                         -- since h x proves
                         -- x ∈ A ↔ x ∈ B, 
                         -- propositions A x and B x are equal
```

Ця теорема встановлює те, про що говорить Расьова: дві множини є рівними тоді й лише тоді, коли вони мають ті самі елементи - базовий принцип екстенсіональності множин.

Маючи формалізованими відношення підмножин і рівність множин, ми готові досліджувати глибші математичні властивості, що випливають із цих означень. Наступний крок у Расьової - показати основні властивості, яким має задовольняти кожна математична структура, побудована на підмножинах, - такі властивості, як рефлексивність, транзитивність і антисиметричність, що розкривають внутрішню логічну архітектуру теорії множин.


**Далі:** [Частина 3 - Властивості підмножин](/lean4/math-textbook-3/)
